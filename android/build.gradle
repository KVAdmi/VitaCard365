// Pin de versiones Kotlin stdlib para TODO el árbol (Gradle 8+)
subprojects {
    configurations.configureEach {
        resolutionStrategy {
            // Opción A: forzar explícito de artefactos stdlib
            force(
                "org.jetbrains.kotlin:kotlin-stdlib:1.9.25",
                "org.jetbrains.kotlin:kotlin-stdlib-common:1.9.25",
                "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.25",
                "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.25"
            )
            // Opción B adicional: por si aparece otro artefacto stdlib raro
            eachDependency { d ->
                if (d.requested.group == "org.jetbrains.kotlin" &&
                        d.requested.name.startsWith("kotlin-stdlib")) {
                    d.useVersion("1.9.25")
                    d.because("Evitar choque removeFirst/removeLast con Android 15")
                }
            }
        }
    }
}
// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.24'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.github.triplet.gradle:play-publisher:3.9.0'
        // NOTE: App dependencies van en app/build.gradle
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Force Java 17 toolchain for all subprojects to avoid mismatched host JDK (21) issues
subprojects { subproj ->
    afterEvaluate {
        if (subproj.plugins.hasPlugin('com.android.application') || subproj.plugins.hasPlugin('com.android.library')) {
            subproj.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Force Kotlin/Java 17 across all subprojects
subprojects {
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions { jvmTarget = "17" }
  }
  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }
}
